' Gambas class file

Inherits Panel

Event CloseBox ''Close signal
Event PlayClick
Event PlayAllClick
Event CancelClick
Event SelectGenre(sData As String)


Private bgImage As PictureBox
Private CoverPic As PictureBox
Private NameLbl As TextLabel
Private InfoLbl As TextLabel
Private playBttn As PictureBox
Private cancelBttn As PictureBox
Private hSkinPath As String
Private hSkinTheme As Settings
Private DataControl As New CData
Private timerClose As Timer
Private segCont As Short
Private sList As ScrollView
Private QType As Boolean
Private GenreItem As CKey
Private KeyList As Object[]
Public hDownloadClient As New HttpClient As "hDownloadClient"
Private $imagebuffer As String


Public Sub _new()
  
  'Leer configuracion del tema
  hSkinPath = DataControl.SkinPath & DataControl.SkinTheme
  hSkinTheme = New Settings(hSkinPath &/ "theme.conf")
  
  bgImage = New PictureBox(Me) As "bgImage"
  With bgImage
    .X = 0
    .Y = 0
    .H = hSkinTheme["InfoBox/H"]
    .W = hSkinTheme["InfoBox/W"]
    .Stretch = True
    .Visible = True
  End With
  
  CoverPic = New PictureBox(Me)
  With CoverPic
    .X = hSkinTheme["InfoBoxCover/X"]
    .Y = hSkinTheme["InfoBoxCover/Y"]
    .H = hSkinTheme["InfoBoxCover/H"]
    .W = hSkinTheme["InfoBoxCover/W"]
    .Stretch = True
    .Visible = False
  End With
  
  NameLbl = New TextLabel(Me)
  With NameLbl
    .X = hSkinTheme["InfoBoxNameLbl/X"]
    .Y = hSkinTheme["InfoBoxNameLbl/Y"]
    .H = hSkinTheme["InfoBoxNameLbl/H"]
    .W = hSkinTheme["InfoBoxNameLbl/W"]
    .Foreground = Color.White
    .Font.Size = 15
    .Visible = False
  End With
  
  playBttn = New PictureBox(Me) As "playBttn"
  With playBttn
    .X = hSkinTheme["InfoBoxPlayBttn/X"]
    .Y = hSkinTheme["InfoBoxPlayBttn/Y"]
    .H = hSkinTheme["InfoBoxPlayBttn/H"]
    .W = hSkinTheme["InfoBoxPlayBttn/W"]
    .Picture = Picture[hSkinPath &/ hSkinTheme["Images/PlayBttn"]]
    .Stretch = True
    .Visible = True
  End With
  
  cancelBttn = New PictureBox(Me) As "cancelBttn"
  With cancelBttn
    .X = hSkinTheme["InfoBoxCancelBttn/X"]
    .Y = hSkinTheme["InfoBoxCancelBttn/Y"]
    .H = hSkinTheme["InfoBoxCancelBttn/H"]
    .W = hSkinTheme["InfoBoxCancelBttn/W"]
    .Picture = Picture[hSkinPath &/ hSkinTheme["Images/CancelBttn"]]
    .Stretch = True
    .Visible = True
  End With
  
  createList()
  
  KeyList = New Object[]
  
  timerClose = New Timer As "timerClose"
  timerClose.Delay = 1000
  
  
End

Private Sub createList()
  
  sList = New ScrollView(Me) As "sList"
  With sList
    .X = hSkinTheme["InfoBoxList/X"]
    .Y = hSkinTheme["InfoBoxList/Y"]
    .H = hSkinTheme["InfoBoxList/H"]
    .W = hSkinTheme["InfoBoxList/W"]
    .Visible = False
  End With
  
End

Public Sub MessageType(value As String)
  
  Select value
    Case 1
      'Pregunta normal de reproducci√≥n
      bgImage.Picture = Picture[hSkinPath &/ hSkinTheme["Images/QuestionBox1"]]
      playBttn.Show
      cancelBttn.Show
      sList.Hide
      CoverPic.Show
      NameLbl.Show
      QType = False
    Case 2
      bgImage.Picture = Picture[hSkinPath &/ hSkinTheme["Images/QuestionBox2"]]
      playBttn.Show
      cancelBttn.Show
      sList.Hide
      CoverPic.Hide
      NameLbl.Hide
      QType = True
    Case 3
      bgImage.Picture = Picture[hSkinPath &/ hSkinTheme["Images/NoCredit"]]
      playBttn.Hide
      cancelBttn.Hide
      sList.Hide
      CoverPic.Hide
      NameLbl.Hide
    Case 4
      bgImage.Picture = Picture[hSkinPath &/ hSkinTheme["Images/QuestionBox3"]]
      playBttn.Hide
      cancelBttn.Hide
      sList.Show
      CoverPic.Hide
      NameLbl.Hide
      QType = False
  End Select
  
  TimerOn
  
End

Public Sub loadMusicInfo(data As Song)
  
  If data.cover = "0" Then
    CoverPic.Picture = Picture[hSkinPath &/ hSkinTheme["Images/CoverDefault"]]
  Else
    DataControl.Skm_Modo = data.type
    CoverPic.Picture = Picture[DataControl.getWorkDir() &/ data.genre &/ data.album &/ data.cover]
  Endif
  
  If data.URL_image <> "" Then
    DownloadPicture(data.URL_image)
  Endif
  Select Right(data.name, 3)
    Case Like "{mp3, MP3, wma, WMA, mp4, MP4, ogg, OGG, m4a, M4A}"
      NameLbl.Text = Left(data.name, -4)
    Case Else
      NameLbl.Text = data.name
  End Select
  
End

Public Sub DownloadPicture($url As String)
  
  ' hDownloadClient.URL = Replace($url, "\r", "")
  If Right($url, 3) = "web" Then $url = $url & "p"
  hDownloadClient.URL = $url
  hDownloadClient.TimeOut = 20
  hDownloadClient.Async = True
  hDownloadClient.Get()
  
  Catch
    Debug Error.Text
  
End

Public Sub hDownloadClient_Read()
  
  Dim sBuffer As String
  
  sBuffer = Read #Last, Lof(Last)
  $imagebuffer &= sBuffer
  
End

Public Sub hDownloadClient_Finished()
  
  Dim webp As String
  
  CoverPic.Picture = Null
  ' Debug
  CoverPic.Picture = Picture.FromString($imagebuffer)
  webp = $imagebuffer
  $imagebuffer = Null
  
  Catch 
    Debug "$imagebuffer webp no compatible " & Error.Text
    Shell "echo " & webp & " | dwebp -o - -- -" For Read As "webpConv"
  
End

Public Sub webpConv_Read()
  
  Dim sBuffer As String
  
  sBuffer = Read #Last, Lof(Last)
  $imagebuffer &= sBuffer
  
End

Public Sub webpConv_Kill()
  
  CoverPic.Picture = Picture.FromString($imagebuffer)
  $imagebuffer = Null
  
End

Public Sub loadList(Skm_Modo As String)
  
  Dim hResult As Result, i, b, c As Short, hItem As Integer
  If KeyList.Count > 0 Then
    KeyList.Clear
    sList.Delete
    createList
  Endif
  
  hResult = DataControl.getGenres(Skm_Modo)
  hItem = hSkinTheme["GenreItem/H"]
  For i = 0 To hResult.Max + 1
    GenreItem = New CKey(sList) As "GenreItem"
    If b = 2 Then
       b = 0
       Inc c
    Endif
    With GenreItem
      .X = b * ((hSkinTheme["GenreItem/W"]) + 2)
      .Y = (hItem * c)
      .Hkey = hItem
      .Wkey = hSkinTheme["GenreItem/W"]
      .imgResPath = hSkinPath &/ hSkinTheme["Images/GenreList"]
      If i = 0 Then
        .LetterKey = "Todos"
      Else
        .LetterKey = hResult["Genero"]
        hResult.MoveNext
      Endif
    End With
    Inc b
    KeyList.Add(GenreItem)
  Next
  
End

Public Sub GenreItem_PulseKey()
  
  Raise SelectGenre(Last.LetterKey)
  
End

Public Sub playBttn_MouseDown()
  
  Select QType
    Case True
      Raise PlayAllClick
    Case False
      Raise PlayClick
  End Select
  
  TimerOff
  
End

Public Sub CancelBttn_MouseDown()
  
  Raise CancelClick
  TimerOff
  
End

Private Sub TimerOn()
  
  segCont = 0
  timerClose.Start
  
End

Private Sub TimerOff()
  
  segCont = 0
  timerClose.Stop
  
End

Public Sub timerClose_Timer()
  
  'auto Close
  If segCont < 15 Then
    Inc segCont
  Else
    Raise CloseBox
    TimerOff
  Endif
  
End
