' Gambas class file

' Manejador de cola de reproducción Playlist.class
' Autor:
'   José Francisco Facundo <sos.sempai@gmail.com>
' Licencia:
'   This program Is Free software; you can redistribute it And / Or modify it under
'   the terms Of the GNU General Public License As Published by the Free Software
'   Foundation; version 3.
' 
'  This program Is Distributed In the hope that it will be useful, but WITHOUT
'  ANY WARRANTY; without even the implied warranty Of MERCHANTABILITY Or FITNESS
'  For A PARTICULAR PURPOSE.See the GNU General Public License For more
'   details.
' 
'  You should have received a Copy Of the GNU General Public License along With
'  this program; If Not, Write To the Free Software Foundation, Inc.,
'  51 Franklin Street, Fifth Floor, Boston, MA 02110 - 1301 USA

Event hMovie
Event sMovie

Private MatrizLed As PanelLed
Private DataControl As New CData
Private $bState As Boolean ''Estado del la reproducción
Private UploadState As Boolean ''Estado de la carga de archivo
Private PlayListSongs As Integer
Private mPlayer As CPlayer
Private hSkinPath As String
Private hSkinTheme As Settings
Private cacheDir As String
Private ResMovieBox As Timer
Private sMovieBox As Boolean
Private iCont As Short
Private inputFile As Boolean
Private UploadSong As String
Private UploadTimer As Timer
Private DownloadTimer As Timer
Private AutoPlay As Timer
Private TimeCont As Integer
Private SongPlaying As String
Private upFailed As Boolean
Private SearchBoxShow As Boolean
Property MatrizShow As Boolean
Private hProcess As Process
Private procStatus As Boolean
Private VideoRes As String = "720"
Private VideoFormat As String = "mp4"
Public DownBar As ProgressBar
Private PlaySong As New Song
Property MovieBoxState As Boolean

 
Public Sub _new()
  
  hSkinPath = DataControl.SkinPath & DataControl.SkinTheme
  hSkinTheme = New Settings(hSkinPath &/ "theme.conf")
  
  MatrizLed = New PanelLed(FMain)
  
  With MatrizLed
    .X = hSkinTheme["ScrollText/X"]
    .Y = hSkinTheme["ScrollText/Y"]
    .H = hSkinTheme["ScrollText/H"]
    .W = hSkinTheme["ScrollText/W"]
    .LedSpeed = 40
  End With
  
  ResMovieBox = New Timer As "ResMovieBox"
  ResMovieBox.Delay = 1000
  
  UploadTimer = New Timer As "UploadTimer"
  UploadTimer.Delay = 10000
  
  DownloadTimer = New Timer As "DownloadTimer"
  DownloadTimer.Delay = 10000
  
  AutoPlay = New Timer As "AutoPlay"
  AutoPlay.Delay = 1000
  AutoPlay.Start

  UpdateScrollMatrizLed
  mPlayer = New CPlayer As "mPlayer"
  $bState = False
  upFailed = False
  
  DownBar = New ProgressBar(FMain)
  With DownBar
    .X = 42
    .Y = 7
    .W = 77
    .H = 21
    .Hide
  End With
  
  If Not Exist(User.Home &/ ".tmpvideos") Then
    Mkdir User.Home &/ ".tmpvideos"
  Endif
  
  PlayListSongs = DataControl.getAllPlaylist()
  
End

Public Sub UpdateScrollMatrizLed()
  
  Dim MessageText As String
  
  MessageText = DataControl.ScrollString
  addMatrizLed(MessageText)
  
End

Private Sub StopMatrizLed()
  
  If DataControl.FtpEnable = True Then MatrizLed.Stop
  
End

Public Function AddPlay(sSong As Song)
  
  
  If PlayListSongs = 0 And $bState = False And UploadState = False And procStatus = False And inputFile = False Then
    inputFile = True
    
    If sSong.type = "vonline" Then
      
        cacheDir = DataControl.CacheVideoDir
        
        If DataControl.SearchCacheID(sSong.ID_video) = False Or Not Exist(cacheDir &/ sSong.ID_video & "." & VideoFormat) Then 
            DownloadFile(sSong)
            While procStatus = True
              Wait 0.3
            Wend
            If mPlayer.PlayProcess = True Then 
                'Si suena una canción mientras descarga, guardo sSong a la playlist
                DataControl.DBPlayList = sSong
                Return
            Endif  
        Endif 
        
        StopMatrizLed
        
        If DataControl.FtpEnable = True Then UploadState = True
        mPlayer.Play(sSong.ID_video & "." & VideoFormat, cacheDir, "Video", True, "0")
        addMatrizLed("** Ahora sonando - " & Left$(sSong.name, -4) & "  **")
        SongPlaying = sSong.name
        PlaySong = sSong
        $bState = True
        If DataControl.SearchCacheID(sSong.ID_video) = False Then 
            DataControl.setCacheDownload(sSong)
        Endif
      
    Else
      
        StopMatrizLed
        DataControl.Skm_Modo = sSong.type
        
        If DataControl.FtpEnable = True Then UploadState = True
            mPlayer.Play(sSong.name, DataControl.getWorkDir() &/ sSong.genre &/ sSong.album, sSong.type, True, sSong.cover)
            addMatrizLed("** Ahora sonando - " & Left$(sSong.name, -4) & " - " & sSong.album & " **")
            SongPlaying = sSong.name
            PlaySong = sSong
            $bState = True
        Endif
        
  Else If PlayListSongs = 0 And procStatus = True And $bState = False
    
      Debug "test"
      $bState = True
      
      If sSong.type = "vonline" Then 
          If DataControl.SearchCacheID(sSong.ID_video) = False Then 
              DataControl.DBPlayList = sSong
              Return 
          Endif
      Endif
      
      StopMatrizLed
      DataControl.Skm_Modo = sSong.type
      If DataControl.FtpEnable = True Then UploadState = True
      mPlayer.Play(sSong.name, DataControl.getWorkDir() &/ sSong.genre &/ sSong.album, sSong.type, True, sSong.cover)
      addMatrizLed("** Ahora sonando - " & Left$(sSong.name, -4) & " - " & sSong.album & " **")
      SongPlaying = sSong.name
      PlaySong = sSong
  Else 
    Inc PlayListSongs
    Debug
    DataControl.DBPlayList = sSong
    
    If PlayListSongs = 1 Then 
        If DataControl.FtpEnable Then
          UploadTimer.Start
        Else 
          DownloadTimer.Start
        Endif 
    Endif 
    
  Endif
  AutoPlay.Stop
  inputFile = False
  
End

Private Sub DownloadFile(sSong As Song)
  
  procStatus = True
  DownBar.Show
  
  ' hProcess = Exec ["youtube-dl", "--newline", "-o", User.Home &/ ".tmpvideos" &/ sSong.ID_video & ".m4a", "-f", "140/m4a/bestaudio", "--no-check-certificate", "http://www.youtube.com/watch?v=" & sSong.ID_video] For Read As "hProcess"
  
  If sSong.ID_video = "" Then Return 
  
  hProcess = Exec ["youtube-dl", "--newline", "-o", cacheDir &/ "o_" & sSong.ID_video & "." & VideoFormat, "-x", "--audio-format", "m4a", "-k", "-f", "best[height<=?" & VideoRes & "]", "--no-check-certificate", "--prefer-insecure", "http://www.youtube.com/watch?v=" & sSong.ID_video] For Read As "hProcess"
  
  While procStatus = True
    Wait 0.3
  Wend
  
  Debug "Gain"
  procStatus = True

  hProcess = Exec ["/usr/bin/gain.sh", cacheDir, sSong.ID_video, VideoFormat] For Read As "fProcess"
  
  While procStatus = True
    Wait 0.3
  Wend
  
  Catch 
    Debug Error.Text
  
End

Public Sub hProcess_Read()
  
  Dim sBuffer As String

  sBuffer = Read #Last, Lof(Last)
  
  Print sBuffer
  
  If Left(sBuffer, 10) = "[download]" Then
    If Mid(sBuffer, 12, 1) <> "D" And If Mid(sBuffer, 12, 1) <> "G" And If Mid(sBuffer, 12, 4) <> Left(cacheDir, 4) Then
      DownBar.Value = CInt(LTrim(Mid(sBuffer, 11, 4))) / 100
    Endif
  Endif
  
  Catch 
    Debug Error.Text

End

Public Sub fProcess_Read()

  Dim sLine As String

  sLine = Read #Last, -256

  Print sLine;

End

Public Sub hProcess_Kill()
  
  Debug
  
  procStatus = False
  
End

Public Sub fProcess_kill()
  
  procStatus = False
  DownBar.Hide
  DownBar.Value = 0
  
End

Public Sub hProcess_Error(Value As String)
  
  Debug Value
  procStatus = False
  DownBar.Hide
  DownBar.Value = 0
  
End

Public Sub mPlayer_ProcessEnd()
  
  Dim upState As Boolean = True
  Dim sSong As Song
  HideMovie
  ResMovieBox.Stop
  sMovieBox = False
  While procStatus = True
    Wait 1
  Wend
  If PlayListSongs > 0 Then
    sSong = DataControl.DBPlayList
    DataControl.DelFirstItemPL
    
    If sSong.type <> "vonline" Then DataControl.Skm_Modo = sSong.type
    
    'Al terminar la reproducción se borra el archivo remoto
    'Si el archivo es el mismo al siguiente en la cola, se mantiene.
    If sSong.name <> SongPlaying Then 
      mPlayer.DeleteFile
    Else
      upState = False
    Endif
    'Si el archivo esta en proceso de envio al servidor, esperar hasta que termine el proceso.
    While UploadState = True
      Wait 1
      Debug "1"
    Wend
    
    'Si el archivo ya esta en el servidor se ignora el flag upState
    If sSong.name = UploadSong And upFailed = False Then upState = False
    StopMatrizLed
    If DataControl.FtpEnable = True Then UploadState = True
    
    If sSong.type <> "vonline" Then
        mPlayer.Play(sSong.name, DataControl.getWorkDir() &/ sSong.genre &/ sSong.album, sSong.type, upState, sSong.cover)
        addMatrizLed("** Ahora sonando - " & sSong.album & " - " & Left$(sSong.name, -4) & " **")
    Else
        cacheDir = DataControl.CacheVideoDir
        
        If DataControl.SearchCacheID(sSong.ID_video) = False Or Not Exist(DataControl.CacheVideoDir &/ sSong.ID_video & "." & VideoFormat) Then
            If upState = True Then
              DownloadFile(sSong)
              While procStatus = True
                Wait 0.3
              Wend
            Endif
        Endif 
        
        mPlayer.Play(sSong.ID_video & "." & VideoFormat, cacheDir, "Video", upState, "0")
        Debug "ok"
        addMatrizLed("** Ahora sonando - " & Left$(sSong.name, -4) & " **")
        
        If DataControl.SearchCacheID(sSong.ID_video) = False Then 
          DataControl.setCacheDownload(sSong)
        Endif
        
      Endif
      
    If sSong.type = "musica" Then 
      If SearchBoxShow = False Then MatrizLed.Show
      HideMovie
      Try ResMovieBox.Stop
    Endif
    
    PlaySong = sSong
    SongPlaying = sSong.name
    Dec PlayListSongs
    If PlayListSongs > 0 Then 
      If DataControl.FtpEnable Then
        UploadTimer.Start
      Else 
        DownloadTimer.Start
      Endif 
    Endif 
    
  Else
    
    $bState = False
    HideMovie
    mPlayer.DeleteFile
    ResMovieBox.Stop
    UploadTimer.Stop
    DownloadTimer.Stop
    AutoPlay.Start
    TimeCont = 0
    mPlayer.mpControl = False
    If SearchBoxShow = False Then MatrizLed.Show
    UpdateScrollMatrizLed
  Endif
  
End

Public Sub DownloadTimer_Timer()

  Dim sSong As Song
  
  If DataControl.FtpEnable = True Then Return 
  
  If procStatus = True Then Return
  DownloadTimer.Stop
  sSong = DataControl.DBPlayList
  
  If sSong.ID_video = "" Then Return 
  
  If DataControl.SearchCacheID(sSong.ID_video) = False Then
    cacheDir = DataControl.CacheVideoDir
    DownloadFile(sSong)
    DataControl.setCacheDownload(sSong)
  Endif 
  
End

Public Sub UploadTimer_Timer()
  
  Dim sSong As Song
  
  'Si la salida de video está desactivado entonces lo ignora.
  If DataControl.FtpEnable = False Then Return
  If UploadState = True Then Return
  If procStatus = True Then Return
    
  'Si el servidor se encuentra en proceso de reinicio, ignorar el envio automático.
  If mPlayer.ResProcess Then Return
  UploadTimer.Stop
  If mPlayer.upProcess = True Or PlayListSongs = 0 Then Return
    
  sSong = DataControl.DBPlayList
  If sSong.name = "" Then Return
  
  If sSong.type <> "vonline" Then DataControl.Skm_Modo = sSong.type
  If sSong.name = SongPlaying Then
    Debug "El archivo ya se encuentra en el servidor, ignorando envio."
  Else
    Debug "Enviando archivo"
    If sSong.type <> "vonline" Then
      StopMatrizLed
      UploadState = True
      'Envio de archivo
      mPlayer.UploadFile(sSong.name, DataControl.getWorkDir() &/ sSong.genre &/ sSong.album &/ sSong.name)
      'Portada de album
      If sSong.cover <> "0" Then
        mPlayer.UploadFile(Left(sSong.name, -3) & Right(sSong.cover, 3), DataControl.getWorkDir() &/ sSong.genre &/ sSong.album &/ sSong.cover)
        upFailed = False
      Endif
    Else
      While procStatus = True
        Wait 1
      Wend
      cacheDir = DataControl.CacheVideoDir
      DownloadFile(sSong)
      While procStatus = True
        Wait 0.3
      Wend
      StopMatrizLed
      mPlayer.UploadFile(sSong.ID_video & "." & VideoFormat, cacheDir &/ sSong.ID_video & "." & VideoFormat)
    Endif
  UploadSong = sSong.name
  Endif
  
End

Public Sub AutoPlay_Timer()
  
  Dim TotalAlbums, idPos, jmpPos, SongID, i As Integer
  Dim hResult As Result
  Dim sSong As New Song
  Dim skmodo As String
  
  '//Load Playlist
  If AutoPlay.Delay = 1000 Then
    If PlayListSongs > 0 Then
      mPlayer_ProcessEnd
      $bState = True
    Endif
    AutoPlay.Delay = 60000
    Return
  Endif
  
  '//Automatic Play
  If TimeCont = 15 Then
    
    i = Rnd(1, 10)
    If i < 6 Then
        skmodo = "Musica"
    Else
        skmodo = "Video"
    Endif
   
    DataControl.Skm_Modo = skmodo
    TotalAlbums = DataControl.getTotalAlbums("*")
    
    If TotalAlbums > 0 Then
      
      hResult = DataControl.getDataAlbum(Rnd(1, TotalAlbums))
      
      With sSong
        .album = hResult["Album"]
        .genre = hResult["Genero"]
        .type = LCase(DataControl.Skm_Modo)
        .cover = hResult["Cover_name"]
      End With
      
      idPos = hResult["Total_songs"]
      
      If idPos > 0 Then
        
        idPos = Rnd(1, idPos)
        For i = 1 To 4
          Select idPos
            Case jmpPos + 1 To i * 25
              SongID = hResult["Songsid" & i]
              jmpPos = (i * 25) - 25
              Break
          End Select
        Next
        
        hResult = DataControl.getDataSongs(SongID)
        
        With sSong
          .id = hResult["id"]
          .idsong = idPos - jmpPos
          .name = hResult["N" & sSong.idsong]
        End With
        
        AddPlay(sSong)
        
      Endif
    Endif
    
    TimeCont = 0
    
  Else
  
    Inc TimeCont
  
  Endif
  
End

Public Sub mPlayer_UploadFinish()
  
  MatrizLed.Start
  UploadState = False
  Debug
  
End

Public Sub mPlayer_UploadFailed()
  
  ' mPlayer_UploadFinish
  MatrizLed.Start
  upFailed = True
  UploadState = False
  
End

Public Sub ResetResMovie()
  
  iCont = 0
  
End

Public Sub HideMovie()
  
  If PlaySong.type = "musica" Then
    ResMovieBox.Stop
    Return
  Endif
  If mPlayer.VisibleMovieBox = True Then mPlayer.VisibleMovieBox = False
  If SearchBoxShow = False Then MatrizLed.Show
  ResMovieBox.Start
  ResetResMovie
  If SearchBoxShow = True Then
    Raise hMovie
  Endif
  
End

Public Sub ResMovie()
  
  If mPlayer.VisibleMovieBox = False Then mPlayer.VisibleMovieBox = True
  MatrizLed.Hide
  ResMovieBox.Stop
  If SearchBoxShow = True Then
    Raise sMovie
  Endif
  sMovieBox = True
  
End

Public Sub CancelSong()
  
  If $bState = True And UploadState = False Then mPlayer.PlayerStop
  
End

Private Sub addMatrizLed(sText As String)
  
  MatrizLed.Text = sText
  
End

Public Sub mPlayer_MovieBoxVisible()
  
  MatrizLed.Hide
  sMovieBox = True
  If SearchBoxShow = True Then
    Raise sMovie
  Endif
  
End

Public Sub mPlayer_MovieBoxHide()
  
  MatrizLed.Show
  sMovieBox = False
  
End

Public Sub mPlayer_MovieBox_click()
  
  HideMovie
  sMovieBox = False
  
End

Public Sub ResMovieBox_Timer()
  
  Inc iCont
  
  If iCont = 10 Then ResMovie
  
End

Public Sub setPSearch(Value As Boolean)
  
  SearchBoxShow = Value
  
End

Private Function MatrizShow_Read() As Boolean

  Return MatrizLed.Visible

End

Private Sub MatrizShow_Write(Value As Boolean)

  MatrizLed.Visible = Value
  Debug

End

Private Function MovieBoxState_Read() As Boolean

  Return sMovieBox

End

Private Sub MovieBoxState_Write(Value As Boolean)

  Debug Value

End
