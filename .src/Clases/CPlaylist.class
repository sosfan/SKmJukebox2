' Gambas class file

' Manejador de cola de reproducción Playlist.class
' Autor:
'   José Francisco Facundo <sos.sempai@gmail.com>
' Licencia:
'   This program Is Free software; you can redistribute it And / Or modify it under
'   the terms Of the GNU General Public License As Published by the Free Software
'   Foundation; version 3.
' 
'  This program Is Distributed In the hope that it will be useful, but WITHOUT
'  ANY WARRANTY; without even the implied warranty Of MERCHANTABILITY Or FITNESS
'  For A PARTICULAR PURPOSE.See the GNU General Public License For more
'   details.
' 
'  You should have received a Copy Of the GNU General Public License along With
'  this program; If Not, Write To the Free Software Foundation, Inc.,
'  51 Franklin Street, Fifth Floor, Boston, MA 02110 - 1301 USA

Private MatrizLed As PanelLed
Private DataControl As New CData
Private $bState As Boolean 
Private PlayListSongs As Integer
Private mPlayer As CPlayer
Private hSkinPath As String
Private hSkinTheme As Settings
Private ResMovieBox As Timer
Private iCont As Short
Private UploadSong As String
Private UploadTimer As Timer
Private SongPlaying As String
Private WaitUpload As Boolean

 
Public Sub _new()

  Dim MessageText As String
  
  hSkinPath = DataControl.SkinPath & DataControl.SkinTheme
  hSkinTheme = New Settings(hSkinPath &/ "theme.conf")
  
  MatrizLed = New PanelLed(FMain)
  
  With MatrizLed
    .X = hSkinTheme["ScrollText/X"]
    .Y = hSkinTheme["ScrollText/Y"]
    .H = hSkinTheme["ScrollText/H"]
    .W = hSkinTheme["ScrollText/W"]
    .LedSpeed = 50
  End With
  
  ResMovieBox = New Timer As "ResMovieBox"
  ResMovieBox.Delay = 1000
  
  UploadTimer = New Timer As "UploadTimer"
  UploadTimer.Delay = 5000

  MessageText = "Bienvenidos a su lugar preferido"
  addMatrizLed(MessageText)
  mPlayer = New CPlayer As "mPlayer"
  $bState = False
  PlayListSongs = DataControl.getAllPlaylist()
  If PlayListSongs > 0 Then
    mPlayer_ProcessEnd
    $bState = True
  Endif
  
End

Public Function AddPlay(sSong As Song)
  
  If PlayListSongs = 0 And $bState = False Then
    DataControl.Skm_Modo = sSong.type
    mPlayer.Play(sSong.name, DataControl.getWorkDir() &/ sSong.genere &/ sSong.album, sSong.type, True, sSong.cover, sSong.album)
    addMatrizLed("** Ahora sonando - " & Left$(sSong.name, -4) & " - " & sSong.album & " **")
    SongPlaying = sSong.name
    $bState = True
  Else If $bState = True Then
    Inc PlayListSongs
    DataControl.DBPlayList = sSong
    If PlayListSongs = 1 And DataControl.VideoOut Then UploadTimer.Start
  Endif
  
End

Public Sub mPlayer_ProcessEnd()
  
  Dim upState As Boolean = True
  
  Dim sSong As Song
  
  If PlayListSongs > 0 Then
    sSong = DataControl.DBPlayList
    DataControl.DelFirstItemPL
    DataControl.Skm_Modo = sSong.type

    'Al terminar la reproducción se borra el archivo remoto
    'Si el archivo es el mismo al siguiente en la cola, se mantiene.
    If sSong.name <> SongPlaying Then 
      mPlayer.DeleteFile
    Else
      upState = False
    Endif
    
    'Si el archivo esta en proceso de envio al servidor, esperar hasta que termine el proceso.
    While mPlayer.upProcess = True
      Wait 1
    Wend
    
    'Si el archivo ya esta en el servidor se ignora el flag upState
    If sSong.name = UploadSong Then upState = False
    
    mPlayer.Play(sSong.name, DataControl.getWorkDir() &/ sSong.genere &/ sSong.album, sSong.type, upState, sSong.cover, sSong.album)
    If sSong.type = "Musica" Then 
      MatrizLed.Show
      Try ResMovieBox.Stop
    Endif
    addMatrizLed("** Ahora sonando - " & sSong.album & " - " & Left$(sSong.name, -4) & " **")
    SongPlaying = sSong.name
    Dec PlayListSongs
    If PlayListSongs >= 1 And DataControl.VideoOut Then UploadTimer.Start
  Else
    $bState = False
    HideMovie
    mPlayer.DeleteFile
    ResMovieBox.Stop
    UploadTimer.Stop
    mPlayer.mpControl = False
    addMatrizLed("Rockola digital - inserte una moneda")
  Endif
  
End

Public Sub UploadTimer_Timer()
  
  Dim sSong As Song
  If mPlayer.ResProcess Then Return
  UploadTimer.Stop
  If mPlayer.upProcess = True Or PlayListSongs = 0 Then Return
    
  sSong = DataControl.DBPlayList
  If sSong.name = "" Then Return
  
  DataControl.Skm_Modo = sSong.type
  If sSong.name = SongPlaying Then
    Debug "El archivo ya se encuentra en el servidor, ignorando envio."
  Else
    Debug "Enviando archivo"
    If sSong.cover <> "0" Then
      mPlayer.UploadFile(Left(sSong.name, -3) & Right(sSong.cover, 3), DataControl.getWorkDir() &/ sSong.genere &/ sSong.album &/ sSong.cover)
      WaitUpload = True
      While WaitUpload = True
        Wait 0.1
      Wend
    Endif
    mPlayer.UploadFile(sSong.name, DataControl.getWorkDir() &/ sSong.genere &/ sSong.album &/ sSong.name)
    UploadSong = sSong.name
  Endif
  
End

Public Sub mPlayer_UploadFinish()
  
  WaitUpload = False
  
End

Public Sub ResetResMovie()
  
  iCont = 0
  
End

Public Sub HideMovie()
  
  If mPlayer.VisibleMovieBox = True Then mPlayer.VisibleMovieBox = False
  MatrizLed.Show
  ResMovieBox.Start
  ResetResMovie
  
End

Public Sub ResMovie()
  
  If mPlayer.VisibleMovieBox = False Then mPlayer.VisibleMovieBox = True
  MatrizLed.Hide
  ResMovieBox.Stop
  
End

Public Sub CancelSong()
  
  mPlayer.PlayerStop
  
End

Private Sub addMatrizLed(sText As String)
  
  MatrizLed.Text = sText
  
End

Public Sub mPlayer_MovieBoxVisible()
  
  MatrizLed.Hide
  
End

Public Sub mPlayer_MovieBox_click()
  
  HideMovie
  
End

Public Sub ResMovieBox_Timer()
  
  Inc iCont
  
  If iCont = 5 Then ResMovie
  
End
